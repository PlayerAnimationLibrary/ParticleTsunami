import org.apache.commons.lang3.StringUtils

plugins {
    id "com.gradleup.shadow" version "9.2.2" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.13-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base.archivesName = "${rootProject.archives_base_name}${StringUtils.capitalize(project.name)}"
    version = "${rootProject.mod_version}+mc.${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        // Fix issue with lwjgl-freetype not being found on macOS
        maven {
            url "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
        maven {
            name = "NeoForged"
            url = "https://maven.neoforged.net/releases"
        }
        maven {
            name = "RedlanceMinecraft"
            url = "https://repo.redlance.org/public"
        }
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release.set 21
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            // TODO
        }
    }
}
